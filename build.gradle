plugins {
    id "com.jfrog.bintray" version "1.6"
    id 'com.palantir.git-version' version '0.5.2'
}

def getVersionWithoutDirtySuffix = { ->
    def lastTag = versionDetails().lastTag
    if (lastTag.endsWith(".dirty")) {
        return lastTag.substring(0, lastTag.indexOf(".dirty"));
    }
    return lastTag;
}

group = 'com.teslagov'
version = getVersionWithoutDirtySuffix()
description = """A Java SDK for ArcGIS REST APIs"""

repositories {
    mavenCentral()
    maven { url "http://repo.maven.apache.org/maven2" }
}

subprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.8
}

ext {
    githubOrgName = 'TeslaGov'
    githubRepoName = 'joan'
    inceptionYear = 2016

    bintrayUser = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    bintrayApiKey = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    bintrayOrgName = 'teslagov'
    bintrayRepoName = 'joan'

    ossrhUser = project.property('nexusUsername')
    ossrhPass = project.property('nexusPassword')
    passPhrase = project.property('signing.password')
    labels = ['esri', 'arcgis', 'rest', 'java', 'sdk']

    slf4jVersion = "1.7.21"
    junitVersion = '4.12'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}

task printGitGradleVersion << {
    println project.version
}

task bintrayUploadAllArtifacts(dependsOn: [':joan-arc:bintrayUpload', ':joan-arc-core:bintrayUpload', ':joan-arc-portal:bintrayUpload']) << {
    println "Done uploading all artifacts to Bintray..."
}